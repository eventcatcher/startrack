;PALASM Design Description

;---------------------------------- Declaration Segment ------------
TITLE    STARTRK3.PDS - 16Bit-Soundsampler STARTRACK fuer VME-Bus-Atari
PATTERN  A            - Hauptsteuerung des Rec/Play-Systems
REVISION 1.0          - Achtung, Prototyp-Version ! - verbessertes SCMS
AUTHOR   Stephan Wilhelm
COMPANY  Myself
DATE     1/9/96

CHIP  _startrk3  MACH435

;---------------------------------- PIN Declarations ---------------
PIN  20         HCLKI      ; 40MHz-Timingclock
PIN  39         MCLK16     ; 16.934 MHz-Takt
PIN  36         MCLK24     ; 24.576 MHz-Takt
PIN  57         SMPREG     ; VME-Adressdekoder von STARTRK1
PIN  68         A7         ; VME-Adressleitung
PIN  62         A6         ; VME-Adressleitung
PIN  7          RESET      ; VME-RESET
PIN  31         CBL        ; Channel Status Block des CS8412
PIN  47         AESDATAI   ; SDATA des CS8412
PIN  41         AESCIN     ; C des CS8412
PIN  26         AESSYNCI   ; FSYNC des CS8412
PIN  28         AESCLKI    ; SCK des 8412
PIN  27         AESMCLKI   ; MCK des 8412
PIN  24         RD0        PAIR RP0 ; Datenausgang des PLAY-FIFO
PIN  82         RD1        PAIR RP1 ; Datenausgang des PLAY-FIFO
PIN  19         RD2        PAIR RP2 ; Datenausgang des PLAY-FIFO
PIN  81         RD3        PAIR RP3 ; Datenausgang des PLAY-FIFO
PIN  18         RD4        PAIR RP4 ; Datenausgang des PLAY-FIFO
PIN  80         RD5        PAIR RP5 ; Datenausgang des PLAY-FIFO
PIN  17         RD6        PAIR RP6 ; Datenausgang des PLAY-FIFO
PIN  79         RD7        PAIR RP7 ; Datenausgang des PLAY-FIFO
PIN  78         RD8        PAIR RP8 ; Datenausgang des PLAY-FIFO
PIN  38         ADATAI     ; SDATA des A/D-Wandlers CS5339
PIN  25         DA_ACKO    ; ACKO des D/A-Wandlers (128x L/R-CLK)
PIN  37         DSPSTD     ; SERIAL-DATA-Ausgang des DSP
PIN  16         D0         ; VME-Datenleitung
PIN  76         D1         ; VME-Datenleitung
PIN  15         D2         ; VME-Datenleitung
PIN  75         D3         ; VME-Datenleitung
PIN  83         D4         ; VME-Datenleitung
PIN  23         EXTMCLK    ; Externer MCLKx256 fÅr externe Synchronisation
PIN  65         PLAYCLKI   ; CLK-Input fÅr PLAY-Register
PIN  3          DSPSC0     COMBINATORIAL ; zum SC0-Eingang des DSP (FSYNC-32)
PIN  50         DSPSC2     COMBINATORIAL ; zum SC2-Eingang des DSP (L/R-16)
PIN  9          DSPSCK     COMBINATORIAL ; zum Clock-Eingang des DSP
PIN  10         DSPSRD     COMBINATORIAL ; zum SERIAL-Read-Eingang des DSP
PIN  33         CKS        COMBINATORIAL ; Master-Clock-SW CS4328,CMODE CS5339
PIN  34         SMPCLK     COMBINATORIAL ; zum XTI CS4328, ICLKD CS5339
PIN  67         DA_SDATA   COMBINATORIAL ; zum SDATAI des CS4328
PIN  29         AESPRO     COMBINATORIAL ; zum PRO des CS8402A
PIN  8          AESFC0     COMBINATORIAL ; zum FC0 des CS8402A
PIN  70         AESFC1     COMBINATORIAL ; zum FC1 des CS8402A
PIN  12         AESMCLKO   COMBINATORIAL ; zum MCK des CS8402A
PIN  4          AESCOUT    COMBINATORIAL ; zum C/SBF des CS8402A
PIN  60         AESDATAO   COMBINATORIAL ; SDATA fÅr CS8402A (Delayed)
PIN  30         DA_SCLK    COMBINATORIAL ; zum SCLK CS8402A und D/A
PIN  51         DA_FSYNC   COMBINATORIAL ; zum FSYNC des CS8402A und CS4328
PIN  66         WD0        COMBINATORIAL ; zum Dateneingang des REC-FIFO
PIN  54         WD1        COMBINATORIAL ; zum Dateneingang des REC-FIFO
PIN  73         WD2        COMBINATORIAL ; zum Dateneingang des REC-FIFO
PIN  72         WD3        COMBINATORIAL ; zum Dateneingang des REC-FIFO
PIN  55         WD4        COMBINATORIAL ; zum Dateneingang des REC-FIFO
PIN  13         WD5        COMBINATORIAL ; zum Dateneingang des REC-FIFO
PIN  71         WD6        COMBINATORIAL ; zum Dateneingang des REC-FIFO
PIN  14         WD7        COMBINATORIAL ; zum Dateneingang des REC-FIFO
PIN  56         PUFWR      COMBINATORIAL ; zum REC-FIFO Write
PIN  35         PUFRD      COMBINATORIAL ; zum PLAY-FIFO Read
PIN  46         RESRDFIFO  COMBINATORIAL ; RESET des REC-FIFO
PIN  69         SH15          REGISTERED ; Shift von FSYNC (REC-FIFO WD8)
PIN  61         ADCLK32       REGISTERED ; 32BIT-CLK fÅr den CS5339
PIN  77         ADLR          REGISTERED ; L/R fÅr den CS5339
PIN  48         SWDIGIN       REGISTERED ; zum Relais OPTO/CINCH
PIN  52         SWSUBFR       REGISTERED ; SEL fÅr den CS8412
PIN  49         SWPROT        REGISTERED ; Schalter SCMS fÅr Digitalkopie
PIN  58         VOLCLK        REGISTERED ; Schalter CLK fÅr Volume Control
PIN  59         VOLDATAI      REGISTERED ; Schalter DATAI fÅr Volume Control
PIN  6          AESC24        REGISTERED ; Frequenz-Angabe durch CBL
PIN  5          AESC25        REGISTERED ; Frequenz-Angabe durch CBL
PIN  45         PREEMPH       REGISTERED ; Schaltet Preemphasis bei CS8402A
PIN  40         PLAYCLKO   COMBINATORIAL ; CLK-Term fÅr PLAY-Register
NODE ?          AESCLR     COMBINATORIAL ; Bits(0+1)die gelîscht werden sollen
NODE ?          FREQCLK0   COMBINATORIAL ; CLK fÅr Anzeige der Samplefrequenz
NODE ?          FREQCLK1   COMBINATORIAL ; CLK fÅr Anzeige der Samplefrequenz
NODE ?          CZCLK      COMBINATORIAL ; Clock fÅr AESCIN-ZÑhler
NODE ?          INPSCLK    COMBINATORIAL ; Eingangstakt
NODE ?          INPSDATA   COMBINATORIAL ; Eingangsdaten
NODE ?          DATACLK    COMBINATORIAL ; Word-Erzeugung fÅr DSP
NODE ?          DAHLP      COMBINATORIAL ; SDATA-PLAY-Erzeugung
NODE ?          XGORES     COMBINATORIAL ; vorEnd RECFIFO-Write,del.Flankenreg
NODE ?          WRFERTIG   COMBINATORIAL ; beendet REC_FIFO Write-Timing
NODE ?          SHCLK      COMBINATORIAL ; Eingangstakt fÅr Shift-FSYNC
NODE ?          KICKSTART  COMBINATORIAL ; fÅr Kickstart des DA-Wandler CS4328
NODE ?          AESC0         REGISTERED ; Status CS8412 Consumer/Professional
NODE ?          AESC8         REGISTERED ; Status CS8412 Category Bit C8
NODE ?          AESC9         REGISTERED ; Status CS8412 Category Bit C9
NODE ?          MCLK128       REGISTERED ; 128x MCLK fÅr CS8402 Consumer-Mode
NODE ?          ADCLK16       REGISTERED ; 16BIT-CLK fÅr einen A/D-Wandler
NODE ?          HOLD          REGISTERED ; HOLD fÅr einen A/D-Wandler
NODE ?          SHD15         REGISTERED ; Shift der seriellen Record-Daten
NODE ?          SHD14         REGISTERED ; Shift der seriellen Record-Daten
NODE ?          SHD13         REGISTERED ; Shift der seriellen Record-Daten
NODE ?          SHD12         REGISTERED ; Shift der seriellen Record-Daten
NODE ?          SHD11         REGISTERED ; Shift der seriellen Record-Daten
NODE ?          SHD10         REGISTERED ; Shift der seriellen Record-Daten
NODE ?          SHD9          REGISTERED ; Shift der seriellen Record-Daten
NODE ?          SHD8          REGISTERED ; Shift der seriellen Record-Daten
NODE ?          SHD7          REGISTERED ; Shift der seriellen Record-Daten
NODE ?          SHD6          REGISTERED ; Shift der seriellen Record-Daten
NODE ?          SHD5          REGISTERED ; Shift der seriellen Record-Daten
NODE ?          SHD4          REGISTERED ; Shift der seriellen Record-Daten
NODE ?          SHD3          REGISTERED ; Shift der seriellen Record-Daten
NODE ?          SHD2          REGISTERED ; Shift der seriellen Record-Daten
NODE ?          SHD1          REGISTERED ; Shift der seriellen Record-Daten
NODE ?          SHD0          REGISTERED ; Shift der seriellen Record-Daten
NODE ?          RP9           REGISTERED ; Speicher fÅr Daten-PlayFifo-Input
NODE ?          RP8           REGISTERED ; Speicher fÅr Daten-PlayFifo-Input
NODE ?          RP7           REGISTERED ; Speicher fÅr Daten-PlayFifo-Input
NODE ?          RP6           REGISTERED ; Speicher fÅr Daten-PlayFifo-Input
NODE ?          RP5           REGISTERED ; Speicher fÅr Daten-PlayFifo-Input
NODE ?          RP4           REGISTERED ; Speicher fÅr Daten-PlayFifo-Input
NODE ?          RP3           REGISTERED ; Speicher fÅr Daten-PlayFifo-Input
NODE ?          RP2           REGISTERED ; Speicher fÅr Daten-PlayFifo-Input
NODE ?          RP1           REGISTERED ; Speicher fÅr Daten-PlayFifo-Input
NODE ?          RP0           REGISTERED ; Speicher fÅr Daten-PlayFifo-Input
NODE ?          SH0           REGISTERED ; Shift von FSYNC-Input um 16Bit
NODE ?          SH1           REGISTERED ; Shift von FSYNC-Input um 16Bit
NODE ?          SH2           REGISTERED ; Shift von FSYNC-Input um 16Bit
NODE ?          SH3           REGISTERED ; Shift von FSYNC-Input um 16Bit
NODE ?          SH4           REGISTERED ; Shift von FSYNC-Input um 16Bit
NODE ?          SH5           REGISTERED ; Shift von FSYNC-Input um 16Bit
NODE ?          SH6           REGISTERED ; Shift von FSYNC-Input um 16Bit
NODE ?          SH7           REGISTERED ; Shift von FSYNC-Input um 16Bit
NODE ?          SH8           REGISTERED ; Shift von FSYNC-Input um 16Bit
NODE ?          SH9           REGISTERED ; Shift von FSYNC-Input um 16Bit
NODE ?          SH10          REGISTERED ; Shift von FSYNC-Input um 16Bit
NODE ?          SH11          REGISTERED ; Shift von FSYNC-Input um 16Bit
NODE ?          SH12          REGISTERED ; Shift von FSYNC-Input um 16Bit
NODE ?          SH13          REGISTERED ; Shift von FSYNC-Input um 16Bit
NODE ?          SH14          REGISTERED ; Shift von FSYNC-Input um 16Bit
NODE ?          SH16          REGISTERED ; Shift von FSYNC-Input um 16Bit
NODE ?          TAKT5         REGISTERED ; Frequenz-Zwischenteiler fÅr A/D
NODE ?          TAKT6         REGISTERED ; Frequenz-Zwischenteiler fÅr A/D
NODE ?          TAKT7         REGISTERED ; Frequenz-Zwischenteiler fÅr A/D
NODE ?          SWCOPY        REGISTERED ; Schalter Full-Copy/1st Generation
NODE ?          SWAES         REGISTERED ; Zwischenspeicher/Schalter DIGI/ANA
NODE ?          SWDIGOUT      REGISTERED ; Zwischensp./Schalter Ausg.IN/PLAY
NODE ?          SWONDSP       REGISTERED ; Zwischensp./Schalter DSP an/aus
NODE ?          SWINDSP       REGISTERED ; Zwischensp./Schalter DSP in/out
NODE ?          SWRESFF       REGISTERED ; Schalter/Auslîser Reset fÅr RecFIFO
NODE ?          SWPROF        REGISTERED ; Schalter Consumer/Prof-Mode
NODE ?          TAKTL         REGISTERED ; Zwischenspeicher/Schalter SMPrate
NODE ?          TAKTH         REGISTERED ; Zwischenspeicher/Schalter SMPrate
NODE ?          EXTAKT        REGISTERED ; Zwischenspeicher/Schalter ext.MCLK
NODE ?          WAIT          REGISTERED ; Record-FIFO-Write-Timing
NODE ?          WAIT2         REGISTERED ; Record-FIFO-Write-Timing
NODE ?          WAIT3         REGISTERED ; Record-FIFO-Write-Timing
NODE ?          WAIT4         REGISTERED ; Record-FIFO-Write-Timing
NODE ?          WAIT5         REGISTERED ; Record-FIFO-Write-Timing
NODE ?          BWAIT         REGISTERED ; Play-FIFO-Read-Timing
NODE ?          BWAIT2        REGISTERED ; Play-FIFO-Read-Timing
NODE ?          BWAIT3        REGISTERED ; Play-FIFO-Read-Timing
NODE ?          Z0            REGISTERED ; Play-SDATAzÑhler und Synchro
NODE ?          Z1            REGISTERED ; Play-SDATAzÑhler und Synchro
NODE ?          Z2            REGISTERED ; Play-SDATAzÑhler und Synchro
NODE ?          Z3            REGISTERED ; Play-SDATAzÑhler und Synchro
NODE ?          Z4            REGISTERED ; Play-SDATAzÑhler und Synchro
NODE ?          Z5            REGISTERED ; Play-SDATAzÑhler und Synchro
NODE ?          CZ0           REGISTERED ; Channel-Status DatenzÑhler
NODE ?          CZ1           REGISTERED ; Channel-Status DatenzÑhler
NODE ?          CZ2           REGISTERED ; Channel-Status DatenzÑhler
NODE ?          CZ3           REGISTERED ; Channel-Status DatenzÑhler
NODE ?          CZ4           REGISTERED ; Channel-Status DatenzÑhler
NODE ?          CZ5           REGISTERED ; Channel-Status DatenzÑhler
NODE ?          PGO           REGISTERED ; Flanken-Erkenner fÅr pos.Flanke
NODE ?          NGO           REGISTERED ; Flanken-Erkenner fÅr neg.Flanke
NODE ?          MCLK12        REGISTERED ; fÅr Frequenzerzeugung

;----------------------------------- Boolean Equation Segment ------
EQUATIONS

; ---------------------Register

SWAES = D0;
/SWAES.CLKF = /SMPREG * /A7 * /A6;
SWAES.RSTF = /RESET;
SWAES.SETF = GND;

SWDIGIN = D1;
/SWDIGIN.CLKF = /SMPREG * /A7 * /A6;
SWDIGIN.RSTF = /RESET;
SWDIGIN.SETF = GND;

TAKTL = D2;
/TAKTL.CLKF = /SMPREG * /A7 * /A6;
TAKTL.RSTF = /RESET;
TAKTL.SETF = GND;

TAKTH = D3;
/TAKTH.CLKF = /SMPREG * /A7 * /A6;
TAKTH.RSTF = /RESET;
TAKTH.SETF = GND;

EXTAKT = D4;
/EXTAKT.CLKF = /SMPREG * /A7 * /A6;
EXTAKT.RSTF = /RESET;
EXTAKT.SETF = GND;

SWPROT = /D0;
/SWPROT.CLKF = /SMPREG * /A7 * A6;
SWPROT.RSTF = /RESET;
SWPROT.SETF = GND;

SWRESFF = D1;
/SWRESFF.CLKF = /SMPREG * /A7 * A6;
SWRESFF.RSTF = /RESET;
SWRESFF.SETF = GND;

SWDIGOUT = D2;
/SWDIGOUT.CLKF = /SMPREG * /A7 * A6;
SWDIGOUT.RSTF = /RESET;
SWDIGOUT.SETF = GND;

SWSUBFR = D3;
/SWSUBFR.CLKF = /SMPREG * /A7 * A6;
SWSUBFR.RSTF = /RESET;
SWSUBFR.SETF = GND;

PREEMPH = /D4;
/PREEMPH.CLKF = /SMPREG * /A7 * A6;
PREEMPH.RSTF = GND;
PREEMPH.SETF = /RESET;

SWONDSP = D0;
/SWONDSP.CLKF = /SMPREG * A7 * /A6;
SWONDSP.RSTF = /RESET;
SWONDSP.SETF = GND;

SWINDSP = D1;
/SWINDSP.CLKF = /SMPREG * A7 * /A6;
SWINDSP.RSTF = /RESET;
SWINDSP.SETF = GND;

VOLCLK = D3;
/VOLCLK.CLKF = /SMPREG * A7 * /A6;
VOLCLK.RSTF = /RESET;
VOLCLK.SETF = GND;

VOLDATAI = D4;
/VOLDATAI.CLKF = /SMPREG * A7 * /A6;
VOLDATAI.RSTF = /RESET;
VOLDATAI.SETF = GND;

SWPROF = D0;
/SWPROF.CLKF = /SMPREG * A7 * A6;
SWPROF.RSTF = /RESET;
SWPROF.SETF = GND;

SWCOPY = D1;
/SWCOPY.CLKF = /SMPREG * A7 * A6;
SWCOPY.RSTF = /RESET;
SWCOPY.SETF = GND;

; ----Erkennung Category-Code und Prof/Consumermode fuer Digitalausgang
; ----Sample-Frequenzerkennung fÅr Status-Anzeige

CZCLK = AESSYNCI
      + CZCLK * CZ5 * CZ4 * CZ3 * CZ2 * CZ1 * CZ0 * CBL;

CZ0.T = CBL;
CZ0.CLKF = CZCLK;
CZ0.RSTF = /CBL;
CZ0.SETF = GND;

CZ1.T = CZ0 * CBL;
CZ1.CLKF = CZCLK;
CZ1.RSTF = /CBL;
CZ1.SETF = GND;

CZ2.T = CZ0 * CZ1 * CBL
CZ2.CLKF = CZCLK;
CZ2.RSTF = /CBL;
CZ2.SETF = GND;

CZ3.T = CZ0 * CZ1 * CZ2 * CBL
CZ3.CLKF = CZCLK;
CZ3.RSTF = /CBL;
CZ3.SETF = GND;

CZ4.T = CZ0 * CZ1 * CZ2 * CZ3 * CBL
CZ4.CLKF = CZCLK;
CZ4.RSTF = /CBL;
CZ4.SETF = GND;

CZ5.T = CZ0 * CZ1 * CZ2 * CZ3 * CZ4 * CBL
CZ5.CLKF = CZCLK;
CZ5.RSTF = /CBL;
CZ5.SETF = GND;

AESC0 = AESCIN;
AESC0.CLKF = /CZ5 * /CZ4 * /CZ3 * /CZ2 * /CZ1 * /CZ0 * CBL;
AESC0.RSTF = GND;
AESC0.SETF = GND;

AESC8 = AESCIN;
AESC8.CLKF = /CZ5 * /CZ4 * CZ3 * /CZ2 * /CZ1 * CZ0 * CBL;
AESC8.RSTF = GND;
AESC8.SETF = GND;

AESC9 = AESCIN;
AESC9.CLKF = /CZ5 * /CZ4 * CZ3 * /CZ2 * CZ1 * /CZ0 * CBL;
AESC9.RSTF = GND;
AESC9.SETF = GND;

FREQCLK0 = /CZ5 * CZ4 * CZ3 * /CZ2 * /CZ1 * CZ0 * CBL * /AESC0
         + /CZ5 * /CZ4 * /CZ3 * CZ2 * CZ1 * CZ0 * CBL * AESC0;

FREQCLK1 = /CZ5 * CZ4 * CZ3 * /CZ2 * CZ1 * /CZ0 * CBL * /AESC0
         + /CZ5 * /CZ4 * CZ3 * /CZ2 * /CZ1 * /CZ0 * CBL * AESC0;

AESC24 = AESCIN;
AESC24.CLKF = FREQCLK0;
AESC24.RSTF = GND;
AESC24.SETF = GND;

AESC25 = AESCIN;
AESC25.CLKF = FREQCLK1;
AESC25.RSTF = GND;
AESC25.SETF = GND;
; ---------------------Reseterzeugung

/RESRDFIFO = /RESET + SWRESFF;

; ---------------------Frequenzerzeugung 12.288 MHz fÅr A/D & D/A

MCLK12.T = VCC;
MCLK12.CLKF = MCLK24;
MCLK12.SETF = GND;
MCLK12.RSTF = GND;

; ---------------------Frequenzerzeugung XTI/ICLKD fÅr D/A und A/D

SMPCLK = /EXTAKT * /TAKTH * /TAKTL * AESMCLKI
       + /EXTAKT * /TAKTH * TAKTL * MCLK12
       + /EXTAKT * TAKTH * /TAKTL * MCLK16
       + /EXTAKT * TAKTH * TAKTL * MCLK12
       + EXTAKT * /EXTMCLK;

; ---------------------Taktumschaltung fÅr A/D und D/A-Samplerate
; ---bei ext. Takt kann Åber TAKTL zw. 256/384fach CLK gewÑhlt werden

CKS = /EXTAKT * /TAKTH * TAKTL
    + /EXTAKT * TAKTH * /TAKTL
    + EXTAKT * TAKTL;

; ---------------------Frequenzerzeugung fÅr A/D-Clock,Hold und L/R

ADCLK32.T = VCC;
ADCLK32.CLKF = DA_ACKO;
ADCLK32.RSTF = GND;
ADCLK32.SETF = GND;

ADCLK16.T = ADCLK32;
ADCLK16.CLKF = DA_ACKO;
ADCLK16.RSTF = GND;
ADCLK16.SETF = GND;

TAKT5.T = ADCLK32 * ADCLK16;
TAKT5.CLKF = DA_ACKO;
TAKT5.RSTF = GND;
TAKT5.SETF = GND;

TAKT6.T = ADCLK32 * ADCLK16 * TAKT5;
TAKT6.CLKF = DA_ACKO;
TAKT6.RSTF = GND;
TAKT6.SETF = GND;

TAKT7.T = ADCLK32 * ADCLK16 * TAKT5 * TAKT6;
TAKT7.CLKF = DA_ACKO;
TAKT7.RSTF = GND;
TAKT7.SETF = GND;

HOLD.T = ADCLK32 * ADCLK16 * TAKT5 * TAKT6 * TAKT7;
HOLD.CLKF = DA_ACKO;
HOLD.RSTF = GND;
HOLD.SETF = GND;

/ADLR.T = ADCLK32 * ADCLK16 * TAKT5 * TAKT6 * TAKT7 * HOLD;
ADLR.CLKF = DA_ACKO;
ADLR.RSTF = GND;
ADLR.SETF = GND;

; ---------------------Eingangsdaten und Eingangstakt

INPSCLK = /AESCLKI * /SWAES
        + ADCLK32 * SWAES;

INPSDATA = AESDATAI * /SWAES
         + ADATAI * ADLR * SH16 * SWAES
         + ADATAI * /ADLR * /SH16 * SWAES;

; ---------------------Shift der FSYNC-Flanken(und dadurch der Daten)um 16Bit

KICKSTART = RESET * SH7
          + RESET * KICKSTART;

SHCLK = AESCLKI * /SWAES
      + /ADCLK32 * SWAES
      + RESET * /SH7 * MCLK16 * /KICKSTART;

SH0 = /AESSYNCI * /SWAES
    + /ADLR * SWAES;
SH0.CLKF = SHCLK;
SH0.SETF = /RESET;
SH0.RSTF = GND;

SH1 = SH0;
SH1.CLKF = SHCLK;
SH1.SETF = GND;
SH1.RSTF = /RESET;

SH2 = SH1;
SH2.CLKF = SHCLK;
SH2.SETF = GND;
SH2.RSTF = /RESET;

SH3 = SH2;
SH3.CLKF = SHCLK;
SH3.SETF = GND;
SH3.RSTF = /RESET;

SH4 = SH3;
SH4.CLKF = SHCLK;
SH4.SETF = GND;
SH4.RSTF = /RESET;

SH5 = SH4;
SH5.CLKF = SHCLK;
SH5.SETF = GND;
SH5.RSTF = /RESET;

SH6 = SH5;
SH6.CLKF = SHCLK;
SH6.SETF = GND;
SH6.RSTF = /RESET;

SH7 = SH6;
SH7.CLKF = SHCLK;
SH7.SETF = GND;
SH7.RSTF = /RESET;

SH8 = SH7;
SH8.CLKF = SHCLK;
SH8.SETF = GND;
SH8.RSTF = GND;

SH9 = SH8;
SH9.CLKF = SHCLK;
SH9.SETF = GND;
SH9.RSTF = GND;

SH10 = SH9;
SH10.CLKF = SHCLK;
SH10.SETF = GND;
SH10.RSTF = GND;

SH11 = SH10;
SH11.CLKF = SHCLK;
SH11.SETF = /RESET;
SH11.RSTF = GND;

SH12 = SH11;
SH12.CLKF = SHCLK;
SH12.SETF = /RESET;
SH12.RSTF = GND;

SH13 = SH12;
SH13.CLKF = SHCLK;
SH13.SETF = GND;
SH13.RSTF = /RESET;

SH14 = SH13;
SH14.CLKF = SHCLK;
SH14.SETF = GND;
SH14.RSTF = /RESET;

SH15 = SH14;
SH15.CLKF = SHCLK;
SH15.SETF = /RESET;
SH15.RSTF = GND;

SH16 = SH15;
SH16.CLKF = SHCLK;
SH16.SETF = /RESET;
SH16.RSTF = GND;

; ---------------------Fsync-Flankenerkennung (erzeugt positive SprÅnge)

PGO = VCC;
PGO.CLKF = SH16;
PGO.SETF = GND;
PGO.RSTF = /XGORES;

NGO = VCC;
/NGO.CLKF = SH16;
NGO.SETF = GND;
NGO.RSTF = /XGORES;

; ---------------------Record

SHD0 = INPSDATA * /SWONDSP
     + INPSDATA * SWINDSP
     + DSPSTD * SWONDSP * /SWINDSP;
SHD0.CLKF = INPSCLK;
SHD0.SETF = GND;
SHD0.RSTF = GND;

SHD1 = SHD0;
SHD1.CLKF = INPSCLK;
SHD1.SETF = GND;
SHD1.RSTF = GND;

SHD2 = SHD1;
SHD2.CLKF = INPSCLK;
SHD2.SETF = GND;
SHD2.RSTF = GND;

SHD3 = SHD2;
SHD3.CLKF = INPSCLK;
SHD3.SETF = GND;
SHD3.RSTF = GND;

SHD4 = SHD3;
SHD4.CLKF = INPSCLK;
SHD4.SETF = GND;
SHD4.RSTF = GND;

SHD5 = SHD4;
SHD5.CLKF = INPSCLK;
SHD5.SETF = GND;
SHD5.RSTF = GND;

SHD6 = SHD5;
SHD6.CLKF = INPSCLK;
SHD6.SETF = GND;
SHD6.RSTF = GND;

SHD7 = SHD6;
SHD7.CLKF = INPSCLK;
SHD7.SETF = GND;
SHD7.RSTF = GND;

SHD8 = SHD7;
SHD8.CLKF = INPSCLK;
SHD8.SETF = GND;
SHD8.RSTF = GND;

SHD9 = SHD8;
SHD9.CLKF = INPSCLK;
SHD9.SETF = GND;
SHD9.RSTF = GND;

SHD10 = SHD9;
SHD10.CLKF = INPSCLK;
SHD10.SETF = GND;
SHD10.RSTF = GND;

SHD11 = SHD10;
SHD11.CLKF = INPSCLK;
SHD11.SETF = GND;
SHD11.RSTF = GND;

SHD12 = SHD11;
SHD12.CLKF = INPSCLK;
SHD12.SETF = GND;
SHD12.RSTF = GND;

SHD13 = SHD12;
SHD13.CLKF = INPSCLK;
SHD13.SETF = GND;
SHD13.RSTF = GND;

SHD14 = SHD13;
SHD14.CLKF = INPSCLK;
SHD14.SETF = GND;
SHD14.RSTF = GND;

SHD15 = SHD14;
SHD15.CLKF = INPSCLK;
SHD15.SETF = GND;
SHD15.RSTF = GND;

WAIT = PGO + NGO;
WAIT.CLKF = HCLKI;
WAIT.RSTF = WRFERTIG;
WAIT.SETF = GND;

WAIT2 = WAIT;
WAIT2.CLKF = HCLKI;
WAIT2.RSTF = WRFERTIG;
WAIT2.SETF = GND;

WAIT3 = WAIT2;
WAIT3.CLKF = HCLKI;
WAIT3.RSTF = WRFERTIG;
WAIT3.SETF = GND;

WAIT4 = WAIT3;
WAIT4.CLKF = HCLKI;
WAIT4.RSTF = WRFERTIG;
WAIT4.SETF = GND;

WAIT5 = WAIT4;
WAIT5.CLKF = HCLKI;
WAIT5.RSTF = WRFERTIG;
WAIT5.SETF = GND;

WRFERTIG = /PGO * /NGO * WAIT * WAIT2 * WAIT3 * WAIT4 * WAIT5 * PUFWR
        + /RESET;

/PUFWR = PGO * WAIT * /WAIT2
       + NGO * WAIT * /WAIT2
       + PGO * WAIT * WAIT2 * WAIT3 * WAIT4 * /WAIT5
       + NGO * WAIT * WAIT2 * WAIT3 * WAIT4 * /WAIT5;

/XGORES = PGO * WAIT * WAIT2 * WAIT3 * WAIT4 * WAIT5 * PUFWR
        + NGO * WAIT * WAIT2 * WAIT3 * WAIT4 * WAIT5 * PUFWR
        + /RESET;

WD0 = SHD8 * PGO * /WAIT3
    + SHD8 * NGO * /WAIT3
    + SHD0 * PGO * WAIT3
    + SHD0 * NGO * WAIT3;

WD1 = SHD9 * PGO * /WAIT3
    + SHD9 * NGO * /WAIT3
    + SHD1 * PGO * WAIT3
    + SHD1 * NGO * WAIT3;

WD2 = SHD10 * PGO * /WAIT3
    + SHD10 * NGO * /WAIT3
    + SHD2 * PGO * WAIT3
    + SHD2 * NGO * WAIT3;

WD3 = SHD11 * PGO * /WAIT3
    + SHD11 * NGO * /WAIT3
    + SHD3 * PGO * WAIT3
    + SHD3 * NGO * WAIT3;

WD4 = SHD12 * PGO * /WAIT3
    + SHD12 * NGO * /WAIT3
    + SHD4 * PGO * WAIT3
    + SHD4 * NGO * WAIT3;

WD5 = SHD13 * PGO * /WAIT3
    + SHD13 * NGO * /WAIT3
    + SHD5 * PGO * WAIT3
    + SHD5 * NGO * WAIT3;

WD6 = SHD14 * PGO * /WAIT3
    + SHD14 * NGO * /WAIT3
    + SHD6 * PGO * WAIT3
    + SHD6 * NGO * WAIT3;

WD7 = SHD15 * PGO * /WAIT3
    + SHD15 * NGO * /WAIT3
    + SHD7 * PGO * WAIT3
    + SHD7 * NGO * WAIT3;

;      WD8 verbunden mit SH15 (Ausgang), ist doch logisch;

; ---------------------Play

Z0.T = VCC;
Z0.CLKF = INPSCLK;    Normalerweise statt INPSCLK -> ADCLK32 wegen getrennter
Z0.RSTF = /RESET;                                    Wiedergabe
Z0.SETF = GND;

Z1.T = Z0;
Z1.CLKF = INPSCLK;
Z1.RSTF = /RESET;
Z1.SETF = GND;

Z2.T = Z0 * Z1;
Z2.CLKF = INPSCLK;
Z2.RSTF = /RESET;
Z2.SETF = GND;

Z3.T = Z0 * Z1 * Z2;
Z3.CLKF = INPSCLK;
Z3.RSTF = /RESET;
Z3.SETF = GND;

Z4.T = Z0 * Z1 * Z2 * Z3;
Z4.CLKF = INPSCLK;
Z4.RSTF = /RESET;
Z4.SETF = GND;

Z5.T = Z0 * Z1 * Z2 * Z3 * Z4;
Z5.CLKF = INPSCLK;
Z5.RSTF = /RESET;
Z5.SETF = GND;

BWAIT = VCC;
BWAIT.CLKF = HCLKI;
BWAIT.RSTF = PUFRD;
BWAIT.SETF = GND;

BWAIT2 = BWAIT;
BWAIT2.CLKF = HCLKI;
BWAIT2.RSTF = PUFRD;
BWAIT2.SETF = GND;

BWAIT3 = BWAIT2;
BWAIT3.CLKF = HCLKI;
BWAIT3.RSTF = PUFRD;
BWAIT3.SETF = GND;

/PUFRD = /Z4 * Z3 * Z2 * /Z1 * /RP8 * /RP9 * /Z5
       + /Z4 * Z3 * Z2 * /Z1 * RP8 * RP9 * Z5
       + Z4 * Z3 * /Z2 * /Z1 * RP8 * /RP9 * /Z5
       + Z4 * Z3 * /Z2 * /Z1 * /RP8 * RP9 * Z5
       + /PUFRD * /Z4 * Z3 * Z2 * /Z1
       + /PUFRD * Z4 * Z3 * /Z2 * /Z1;

; RP8 ist Eingang fÅr Parity

PLAYCLKO = BWAIT3 * /PUFRD;

RP9 := RP8;
RP9.CLKF = PLAYCLKO;

RP8 := RD8;
RP8.CLKF = PLAYCLKI;

RP7 := RD7;
RP7.CLKF = PLAYCLKI;

RP6 := RD6;
RP6.CLKF = PLAYCLKI;

RP5 := RD5;
RP5.CLKF = PLAYCLKI;

RP4 := RD4;
RP4.CLKF = PLAYCLKI;

RP3 := RD3;
RP3.CLKF = PLAYCLKI;

RP2 := RD2;
RP2.CLKF = PLAYCLKI;

RP1 := RD1;
RP1.CLKF = PLAYCLKI;

RP0 := RD0;
RP0.CLKF = PLAYCLKI;

; ---------------------SCLK fÅr D/A und Digital-Ausgang

DA_SCLK = INPSCLK * /SWDIGOUT
       + /INPSCLK * SWDIGOUT;

; ---------------------SDATA fÅr D/A und Digital-Ausgang

DAHLP = RP7 * Z4 * /Z2 * /Z1 * /Z0 * SWDIGOUT
      + RP6 * Z4 * /Z2 * /Z1 * Z0 * SWDIGOUT
      + RP5 * Z4 * /Z2 * Z1 * /Z0 * SWDIGOUT
      + RP4 * Z4 * /Z2 * Z1 * Z0 * SWDIGOUT
      + RP3 * Z4 * Z2 * /Z1 * /Z0 * SWDIGOUT
      + RP2 * Z4 * Z2 * /Z1 * Z0 * SWDIGOUT
      + RP1 * Z4 * Z2 * Z1 * /Z0 * SWDIGOUT
      + RP0 * Z4 * Z2 * Z1 * Z0 * SWDIGOUT;

DA_SDATA = AESDATAI * /SWAES * /SWDIGOUT * /SWONDSP
         + ADATAI * ADLR * SH16 * SWAES * /SWDIGOUT * /SWONDSP
         + ADATAI * /ADLR * /SH16 * SWAES * /SWDIGOUT * /SWONDSP
         + DAHLP * /SWINDSP
         + DAHLP * /SWONDSP
         + DSPSTD * SWONDSP * /SWDIGOUT
         + DSPSTD * SWONDSP * SWINDSP * SWDIGOUT;

; ---------------------FSYNC fÅr D/A und Digital-Ausgang

DA_FSYNC = SH16 * /SWDIGOUT
         + Z5 * SWDIGOUT;

; ---------------------Digital-Ausgang CS8402

AESCLR = /CZ5 * /CZ4 * /CZ3 * /CZ2 * /CZ1 * CZ0 * CBL * /SWPROT * /SWPROF
       + /CZ5 * /CZ4 * /CZ3 * /CZ2 * CZ1 * /CZ0 * CBL * /SWPROT * /SWPROF
       + /CZ5 * CZ4 * /CZ3 * /CZ2 * /CZ1 * /CZ0 * CBL * /AESC0 * SWCOPY
        * /SWPROT
       + /CZ5 * /CZ4 * CZ3 * /CZ2 * CZ1 * CZ0 * CBL * /AESC0 * /SWCOPY
       + /CZ5 * /CZ4 * CZ3 * CZ2 * CBL * /AESC0 * /SWCOPY
       + /CZ5 * /CZ4 * CZ3 * CBL * /AESC0 * SWCOPY * /SWPROT
       + /CZ5 * /CZ4 * /CZ3 * /CZ2 * CZ1 * CZ0 * CBL * SWPROT * /AESC0
       * /SWCOPY
       + /CZ5 * CZ4 * /CZ3 * /CZ2 * /CZ1 * /CZ0 * CBL * SWPROT * /AESC0
       * /AESC8 * /SWCOPY
       + /CZ5 * CZ4 * /CZ3 * /CZ2 * /CZ1 * /CZ0 * CBL * SWPROT * /AESC0
       * /AESC9 * /SWCOPY;

AESCOUT = /CZ5 * /CZ4 * /CZ3 * /CZ2 * CZ1 * CZ0
         * CBL * /SWAES * /SWDIGOUT * /SWPROT * /EXTAKT * /AESC0
        + /CZ5 * /CZ4 * CZ3 * /CZ2 * /CZ1 * CZ0
         * CBL * /SWAES * /SWDIGOUT * /EXTAKT * /AESC0 * /SWCOPY
        + /CZ5 * /CZ4 * CZ3 * /CZ2 * CZ1 * /CZ0
         * CBL * /SWAES * /SWDIGOUT * /EXTAKT * /AESC0 * /SWCOPY
         * AESC8 * AESC9
        + /CZ5 * CZ4 * /CZ3 * /CZ2 * /CZ1 * /CZ0 * CBL * SWPROT * /AESC0
         * AESC8 * AESC9 * /SWCOPY * /SWAES * /SWDIGOUT * /EXTAKT
        + AESCIN * /SWAES * /SWDIGOUT * /EXTAKT * /AESCLR;

MCLK128.T = VCC;
MCLK128.CLKF = AESMCLKI;
MCLK128.SETF = GND;
MCLK128.RSTF = GND;

AESMCLKO = AESMCLKI * /SWAES * /SWDIGOUT * /EXTAKT
         + DA_ACKO * /SWAES * /SWDIGOUT * EXTAKT
         + DA_ACKO * SWAES * /SWDIGOUT
         + DA_ACKO * SWDIGOUT * TAKTH
         + DA_ACKO * SWDIGOUT * TAKTL
         + DA_ACKO * SWDIGOUT * /TAKTH * /TAKTL * EXTAKT
         + MCLK128 * SWDIGOUT * /TAKTH * /TAKTL * /EXTAKT;

AESDATAO = DA_SDATA;

; ---------------------Umschaltung in Consumer-Mode fÅr A/D und PLAY

AESPRO = SWAES * /SWDIGOUT * /SWPROF
       + SWDIGOUT * /SWPROF
       + EXTAKT * /SWPROF;

AESFC0 = TAKTH * TAKTL
       + /TAKTH * /TAKTL * /AESC24 * AESC25
       + SWPROF;

AESFC1 = /TAKTH * TAKTL * /SWPROF
       + /TAKTH * /TAKTL * AESC24 * AESC25 * /SWPROF
       + /SWAES * /SWDIGOUT * /EXTAKT;

; ---------------------Word-Erzeugung fÅr DSP-Eingang

DATACLK = AESSYNCI * SH16 * /SWAES
        + /AESSYNCI * /SH16 * /SWAES
        + ADLR * SH16 * SWAES
        + /ADLR * /SH16 * SWAES;

; ---------------------DSP-Serial-Port

DSPSC0 = SH16 * /SWDIGOUT
       + SH16 * /SWONDSP
       + SH16 * /SWINDSP
       + Z5 * SWDIGOUT * SWONDSP * SWINDSP;

DSPSC2 = DATACLK * /SWDIGOUT
       + DATACLK * /SWONDSP
       + DATACLK * /SWINDSP
       + Z4 * SWDIGOUT * SWONDSP * SWINDSP;

DSPSCK = /INPSCLK * /SWDIGOUT
       + /INPSCLK * /SWONDSP
       + /INPSCLK * /SWINDSP
       + INPSCLK * SWDIGOUT * SWONDSP * SWINDSP;

DSPSRD = INPSDATA * /SWDIGOUT
       + INPSDATA * /SWONDSP
       + INPSDATA * /SWINDSP
       + DAHLP * SWDIGOUT * SWONDSP * SWINDSP;

;----------------------------------- Simulation Segment ------------
SIMULATION

;-------------------------------------------------------------------
