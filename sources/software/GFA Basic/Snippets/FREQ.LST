ref=24.576*1000^2
' chz=40000
CLS
INPUT "hz:";chz
'
@clockcalc(ref,chz)
'
PRINT "f(vco):",freqvco
PRINT "f(vco)/f(ref):",faktor
PRINT
PRINT "Mux:",mux
PRINT "P:",p
PRINT "Q:",q
PRINT "PPM(err):",ppm
PRINT
PRINT "f(vco):",freqout
PRINT "f(hz):",freqhz
PRINT
'
@progword(p,1,mux,q,index)
PRINT "Progword:",HEX$(pw),BIN$(pw)
PRINT
'
PRINT "Send control:",
@sendcontrol(&X1111000000101)
PRINT "Send protocol:",
@sendprotocol(pw)
PRINT "Send control:",
@sendcontrol(&X1111000000100)
PAUSE 2
PRINT "Send control:",
@sendcontrol(&X1111000000000)
'
END
'
> PROCEDURE clockcalc(freqref,hz)
  freqout=hz*512
  freqvco=freqout
  mux=0
  WHILE freqvco<(50*1000^2) AND mux<7
    INC mux
    freqvco=freqout*2^mux
  WEND
  faktor=freqvco/(freqref*2)
  pv=3
  p=3
  q=3
  REPEAT
    INC pv
    qv=INT(pv/faktor)
    IF qv<3 THEN
      qv=3
    ENDIF
    IF qv>129 THEN
      qv=129
    ENDIF
    IF (ABS((pv/qv)-faktor))<=(ABS((p/q)-faktor)) THEN
      p=pv
      q=qv
    ENDIF
    INC qv
    IF qv>129 THEN
      qv=129
    ENDIF
    IF (ABS((pv/qv)-faktor))<=(ABS((p/q)-faktor)) THEN
      p=pv
      q=qv
    ENDIF
  UNTIL pv=130
  ppm=INT((1000^2)*ABS((p/q)-faktor))
  freqout=INT(2*freqref*(p/q))
  freqhz=INT(freqout/(2^mux)/512)
  index=0
  IF freqout>80*1000^2 THEN
    index=&X1000
  ENDIF
RETURN
'
> PROCEDURE progword(p,duty,mux,q,index)
  pw=index+(q-2)*&X10000+mux*&X100000000000+duty*&X100000000000000+(p-3)*&X1000000000000000
RETURN
'
> PROCEDURE sendcontrol(cwv)
  FOR i=0 TO 13
    bit=(cwv AND 1)
    cwv=SHR(cwv,1)
    PRINT bit;
  NEXT i
  PRINT
RETURN
'
> PROCEDURE sendprotocol(pwv)
  j=0
  FOR i=0 TO 21
    bit=(pwv AND 1)
    pwv=SHR(pwv,1)
    IF bit=1 THEN
      INC j
    ELSE
      j=0
    ENDIF
    IF j=4 THEN
      PRINT "0";
      j=1
    ENDIF
    PRINT bit;
  NEXT i
  PRINT
RETURN
