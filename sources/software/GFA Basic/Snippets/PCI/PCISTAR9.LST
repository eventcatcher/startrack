CLS
adr=&HA0080000    ! Config PLX
adr2=&H81000000   ! PLX Registers Memory Mapped         ,80H
adr3=&HB0001000   ! PLX Registers I/O Mapped            ,80H
adr4=&HB0001100   ! PLX Local Adress Space I/O Mapped   ,100H
'
@init
'
@wordintel(adr+2,device)
@wordintel(adr,vendor)
POKE adr+4,3                  ! enables CS for memory and io-space in the command-register
@wordintel(adr+6,status)
@wordintel(adr+4,command)
'
@longintel(adr+&H10,base0)  ! Base Adress Memory Mapped Registers
@longintel(adr+&H14,base1)  ! Base Adress I/O Mapped Registers
@longintel(adr+&H18,base2)  ! Base Adress Local Adress Space
@longintel(adr+&H1C,base3)
@longintel(adr+&H20,base4)
@longintel(adr+&H24,base5)
'
'
longwrite(adr2+4,&H2000001)  ! Remap to IC3 /SELM Adress Space
'
@longwrite(adr4,1)
@longwrite(adr4+4,0)
'
ax=&H80
ax2=&HC0
'
REPEAT
  w=0
  REPEAT
    INC w
    @longwrite(adr4+ax,w)
    @longintel(adr4+4,flag)
    IF (flag AND 4)=4 THEN
      fflag=1
    ELSE
      fflag=0
    ENDIF
  UNTIL fflag=0
  IF z=0 THEN
    PRINT "Writes:";w
  ENDIF
  '
  r=0
  REPEAT
    INC r
    @longintel(adr4+ax,y)
    @longintel(adr4+4,flag)
    IF (flag AND 2)=2 THEN
      eflag=1
    ELSE
      eflag=0
    ENDIF
  UNTIL eflag=0
  IF z=0 THEN
    PRINT "Reads:";r
    PRINT
  ENDIF
  IF r<>w THEN
    PRINT "Error (round/w/r) :",z+1,w,r
  ENDIF
  INC z
UNTIL MOUSEK
'
END
'
> PROCEDURE init
  pmem=MALLOC(4)
RETURN
'
> PROCEDURE longintel(memadr,VAR a)
  LPOKE pmem,LPEEK(memadr)
  a1=PEEK(pmem)
  a2=PEEK(pmem+1)
  a3=PEEK(pmem+2)
  a4=PEEK(pmem+3)
  POKE pmem,a4
  POKE pmem+1,a3
  POKE pmem+2,a2
  POKE pmem+3,a1
  a=LPEEK(pmem)
RETURN
'
> PROCEDURE wordintel(memadr,VAR a)
  DPOKE pmem,DPEEK(memadr)
  a1=PEEK(pmem)
  a2=PEEK(pmem+1)
  POKE pmem,a2
  POKE pmem+1,a1
  a=DPEEK(pmem)
RETURN
'
> PROCEDURE longwrite(memadr,a)
  LPOKE pmem,a
  a1=PEEK(pmem)
  a2=PEEK(pmem+1)
  a3=PEEK(pmem+2)
  a4=PEEK(pmem+3)
  POKE pmem,a4
  POKE pmem+1,a3
  POKE pmem+2,a2
  POKE pmem+3,a1
  LPOKE memadr,LPEEK(pmem)
RETURN
