'
' DSP-Router : routes the DSP of the StarTrack Audiocard to
'              Falcon-compatible adress-space (MMU-program)
'
' route FFFFA000 -> FEFF2000 (DSP-Register) pagesize 2K
' route 00FFA000 -> FEFF2000 (DSP-Register) pagesize 2K
'
INLINE rsc_flyd%,3090
rsc_cookie%=ADD(rsc_flyd%,INT{ADD(rsc_flyd%,20)})      ! COOKIE-Routine...
mtype=INT(C:rsc_cookie%(L:CVL("_MCH"))/&H10000)          ! COOKIE-ABFRAGE Maschinentyp : 0,1=ST   2=TT,Medusa
magicmac=INT(C:rsc_cookie%(L:CVL("MgMc")))          ! COOKIE-ABFRAGE Maschinentyp : 0,1=ST   2=TT,Medusa
magix=INT(C:rsc_cookie%(L:CVL("MagX")))          ! COOKIE-ABFRAGE Maschinentyp : 0,1=ST   2=TT,Medusa
cpu=INT(C:rsc_cookie%(L:CVL("_CPU")))          ! COOKIE-ABFRAGE CPU : 30=68030er
'
IF cpu=30 AND magicmac=0 AND magix=0 AND mtype=2 THEN
  IF XBIOS(500,1)=&H71273800 THEN
    IF XBIOS(500,10)>0 THEN
      '
      tabadr=XBIOS(500,407)             ! memory for new MMU tables
      IF tabadr>0 AND XBIOS(500,1)=&H71273800 THEN
        tabadr=tabadr+1024
        tabadr=INT(tabadr/1024)*1024
        rootadr=tabadr        ! memory for rootpointer and translation control
        tabadr=tabadr+1024
        '
      anfdata:
        DATA &H74a,&H10000001,&H20000001,&H30000001,&H40000001,&H50000001,&H60000001,&H70000001
        DATA &H80000041,&H90000041,&Ha0000041,&Hb0000041,&Hc0000041,&Hd0000041,&He0000041,&H78a
        '
        DATA &H7ca,&H1000001,&H2000001,&H3000001,&H4000001,&H5000001,&H6000001,&H7000001
        DATA &H8000001,&H9000001,&Ha000001,&Hb000001,&Hc000001,&Hd000001,&He000001,&Hf000001
        '
        DATA &H41,&H1000041,&H2000041,&H3000041,&H4000041,&H5000041,&H6000041,&H7000041
        DATA &H8000041,&H9000041,&Ha000041,&Hb000041,&Hc000041,&Hd000041,&He000041,&H7ca
        '
        DATA &H19,&H100019,&H200019,&H300019,&H400001,&H500001,&H600001,&H700001
        DATA &H800001,&H900001,&Ha00001,&Hb00001,&Hc00001,&Hd00001,&He00009,&Hf00059
        '
        RESTORE anfdata
        '
        FOR i=0 TO &HFF STEP 4
          READ j
          j=BCLR(j,3)
          j=BCLR(j,4)
          LPOKE tabadr+i,j
        NEXT i
        '
        ' Table A  -> Offset 0
        ' Table B0 -> 0x xx xx xx -> Offset $40
        ' Table B1 -> Fx xx xx xx -> Offset $80
        ' Table C0 -> 00 xx xx xx -> Offset $c0
        ' Table C1 -> FF xx xx xx -> Offset $100
        '
        LPOKE tabadr,tabadr+&H4A       ! 0x xx xx xx link Table A -> Table B0
        LPOKE tabadr+&H3C,tabadr+&H8A  ! Fx xx xx xx link Table A -> Table B1
        '
        '
        ' copy Table C for FF xx xx xx
        j=0
        FOR i=tabadr+&H100 TO tabadr+&H13F STEP 4
          c=LPEEK(tabadr+&HC0+j*4)
          c=BCLR(c,3)
          c=BCLR(c,4)
          LPOKE i,c
          INC j
        NEXT i
        '
        LPOKE tabadr+&H40,tabadr+&HCA    ! 00 xx xx xx link Table B0 -> Table C0
        LPOKE tabadr+&HBC,tabadr+&H10A   ! FF xx xx xx link Table B1 -> Table C1
        '
        '
        ' Table D for 00 Fx xx xx
        j=0
        FOR i=tabadr+&H200 TO tabadr+&H3FF STEP 4
          LPOKE i,&H2000*j+&H41
          INC j
        NEXT i
        LPOKE tabadr+&H200+500,&HFEFF2041    ! new Adress
        LPOKE tabadr+&HFC,tabadr+&H20A       ! 00 Fx xx xx link Table C0 -> Table D0
        '
        ' Table D for FF Fx xx xx
        j=0
        FOR i=tabadr+&H400 TO tabadr+&H5FF STEP 4
          LPOKE i,&H2000*j+&HF00041
          INC j
        NEXT i
        LPOKE tabadr+&H400+500,&HFEFF2041    ! new Adress
        LPOKE tabadr+&H13C,tabadr+&H40A      ! FF Fx xx xx link Table C1 -> Table D1
        '
        '
        LPOKE rootadr,&H80000002
        LPOKE rootadr+4,tabadr
        ~XBIOS(500,404,L:rootadr)        ! set new route
        '
        LPOKE rootadr+8,&H80D04447
        LPOKE rootadr+12,0
        ~XBIOS(500,405,L:rootadr+8)      ! set new translation register
        '
        ' route FFFFA000 -> FEFF2000 (DSP-Register) pagesize 2K
        ' route 00FFA000 -> FEFF2000 (DSP-Register) pagesize 2K
        '
        PRINT
        PRINT "dsproute successful"
        PRINT
      ELSE
        PRINT
        PRINT "dsproute failed, no memory !"
        PRINT
      ENDIF
    ELSE
      PRINT
      PRINT "dsproute failed, no audiocard found !"
      PRINT
    ENDIF
  ELSE
    PRINT
    PRINT "dsproute failed, no audiodriver found !"
    PRINT
  ENDIF
ELSE
  IF cpu<>30 THEN
    PRINT "dsproute failed, wrong cpu (only 68030) !"
  ENDIF
  IF mtype<>2 THEN
    PRINT "dsproute failed, wrong machine (only TT) !"
  ENDIF
  IF magix<>0
    PRINT "dsproute failed, wrong operating system (only TOS, MagiC not supported) !"
  ENDIF
  IF magicmac<>0 THEN
    PRINT "dsproute failed, wrong operating system (only TOS, MagiCMac not supported) !"
  ENDIF
ENDIF
END
